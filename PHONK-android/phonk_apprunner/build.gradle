//apply plugin: 'android'
apply plugin: 'com.android.library'


android {
    flavorDimensions "default_apprunner"

    compileSdkVersion project.compileSdkVersion

    defaultPublishConfig "normalrunnerDebug"
    publishNonDefault true

    defaultConfig {
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES.txt', 'META-INF/LICENSE.txt', 'META-INF/NOTICE.txt', 'META-INF/NOTICE', 'META-INF/LICENSE', 'META-INF/DEPENDENCIES', 'META-INF/notice.txt', 'META-INF/license.txt', 'META-INF/dependencies.txt', 'META-INF/LGPL2.1']
        }
    }


    dexOptions {
        jumboMode true
    }
    androidResources {
        noCompress 'tflite'
    }
}


repositories {
    maven { url 'https://github.com/kshoji/USB-MIDI-Driver/raw/master/MIDIDriver/snapshots' }

    google()
    mavenCentral()
}


dependencies {
    // android support
    api "androidx.appcompat:appcompat:1.5.1"
    api "androidx.recyclerview:recyclerview:1.2.1"
    api "androidx.cardview:cardview:1.0.0"
    api "androidx.palette:palette:1.0.0"
    api "com.google.android.material:material:1.6.1"
    api "androidx.browser:browser:1.4.0"

    // some library seems to use this ¯\_(ツ)_/¯
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'

    //
    api 'org.greenrobot:eventbus:3.2.0'
    api 'org.mozilla:rhino:1.7.13'

    // network
    api group: 'org.java-websocket', name: 'Java-WebSocket', version: '1.5.3'
    api 'de.sciss:netutil:1.0.0'                            //
    api 'org.fusesource.mqtt-client:mqtt-client:1.12'       //
    api group: 'org.eclipse.paho', name: 'org.eclipse.paho.client.mqttv3', version: '1.2.4'
    api 'org.nanohttpd:nanohttpd:2.3.1'
    api group: 'com.jcraft', name: 'jsch', version: '0.1.54'
    implementation 'com.github.weliem:blessed-android:1.20'
    api group: 'org.apache.commons', name: 'commons-lang3', version: '3.10'
    api group: 'commons-net', name: 'commons-net', version: '3.6'
    api group: 'org.apache.ftpserver', name: 'ftpserver-core', version: '1.1.1'
    api group: 'commons-io', name: 'commons-io', version: '2.7'

    // media
    api 'org.puredata.android:pd-core:1.1.0'
    api files('libs/processing_core.jar')

    api 'com.github.japgolly.android:svg-android:2.0.6' //'files('libs/androidsvg-1.2.1.jar')

    // osmdroid
    api 'org.osmdroid:osmdroid-android:6.1.8'
    // api 'com.github.MKergall.osmbonuspack:OSMBonusPack:v5.7'

    // hardware stuff
    api group: 'com.github.ytai.ioio', name: 'IOIOLibAndroid', version: '5.07'
    api group: 'com.github.ytai.ioio', name: 'IOIOLibCore', version: '5.07'
    api group: 'com.github.ytai.ioio', name: 'IOIOLibAndroidBluetooth', version: '5.07'
    api group: 'com.github.ytai.ioio', name: 'IOIOLibAndroidAccessory', version: '5.07'
    api group: 'com.github.ytai.ioio', name: 'IOIOLibAndroidDevice', version: '5.07'
    implementation 'com.github.felHR85:UsbSerial:6.1.0'
    api 'jp.kshoji:midi-driver:0.1.4:@aar'

    // other libs
    api 'net.lingala.zip4j:zip4j:1.3.2'
    api 'com.google.code.gson:gson:2.8.6'
    api group: 'com.google.zxing', name: 'core', version: '3.3.0'
    api group: 'com.google.zxing', name: 'android-core', version: '3.3.0' //
    api 'com.squareup.okhttp3:okhttp:3.10.0' //
    api 'com.squareup.picasso:picasso:2.5.2' //

    // tensorflow
    implementation project(path: ':transfer_api')
    implementation 'org.tensorflow:tensorflow-lite:0.0.0-nightly'
}


// The code below downloads a transfer learning model used in the example app automatically.
// Remove it if you are using your own model!

def modelUrl = 'https://storage.googleapis.com/download.tensorflow.org/models/tflite/transfer_learning_mobilenet_v2_100_224x224_quant_softmax_4_sgd.zip'
def modelArchivePath = "${buildDir}/model.zip"
def modelTargetLocation = 'src/main/assets/model'

task downloadModel(type: DefaultTask) {
    doFirst {
        println "Downloading and unpacking the model..."
        mkdir project.buildDir
    }

    doLast {
        ant.mkdir(dir: modelTargetLocation)
        // If you want to use your own models rather than pre-built models,
        // comment out the following three lines.
        ant.get(src: modelUrl, dest: modelArchivePath)
        ant.unzip(src: modelArchivePath, dest: modelTargetLocation)
        ant.delete(file: modelArchivePath)
    }
}

preBuild.dependsOn downloadModel
